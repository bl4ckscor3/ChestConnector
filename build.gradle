plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
}

version = '1.0.3'
group = 'bl4ckscor3.mod.wrenchest'

base {
	archivesName = 'wrenchest-1.21'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run/client')
    }

    server {
        workingDirectory project.file('run/server')
        programArgument '-nogui'
    }

    data {
		workingDirectory project.file('run/data')
		programArguments.addAll '--mod', 'wrenchest', '--all', '--output', file('src/generated/resources/').getAbsolutePath()
	}
}

dependencies {
	implementation 'net.neoforged:neoforge:21.0.76-beta'
}

tasks.named('jar', Jar).configure {
	exclude('bl4ckscor3/mod/wrenchest/datagen/**') //exclude files from the built jar that are only used to generate the assets & data

    manifest {
        attributes(["Specification-Title": "Wrenchest",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Wrenchest",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
	options.release = 21
}